%%[

/* AMPscript needs to be placed first to extract the parameters from the email. Ideally, parameter would be encrypted in source, and decrypted using DecryptSymmetric to prevent data retrieval via URL hacking.*/

var @id, @email, @firstname, @lastname, @CRMobject, @subscriberstatus, @pageState, @subscriber, @existingemail, @msg, @phone, @campaigns, @campaignid, @campaignname, @i, @CRMobject, @CampaignMember, @IsChecked

Set @id = QueryParameter('id')
Set @msg = QueryParameter('msg')
Set @POSTurl = "https://cloud.mail-powerscore.com/newhandlertest"

/* Get the subscriber's current status */
If not empty(@id) then 
]%%
  <script runat="server"> 
  Platform.Load("core","1");
  var sk = Variable.GetValue("@id");
  var prox = new Script.Util.WSProxy();
  var cols = ["Id","Status","EmailAddress"];
  var filter = {
    Property:"SubscriberKey",
    SimpleOperator:"equals"
    };
  filter.Value = (sk);
  var response = prox.retrieve("Subscriber", cols, filter);
  //Write(Stringify(response));
  Variable.SetValue("@subscriberstatus",response.Results[0].Status);
  Variable.SetValue("@existingemail",response.Results[0].EmailAddress);
  </script>

%%[ 

  /*Get data from Sales Cloud */

  /* Identify if lead or contact */
  if @subscriberstatus == "active" then
    If Substring(@id,1,3) == "003" then
      Set @CRMobject = "Contact"
    elseif Substring(@id,1,3) == "00Q" then
      Set @CRMobject = "Lead" 
    else 
    endif

   Set @subscriber = Row(RetrieveSalesforceObjects(@CRMobject,"Email,FirstName,LastName,Phone","ID","=",@id),1)
   Set @email = Field(@subscriber,'Email')
   Set @firstname = Field(@subscriber,'FirstName')
   Set @lastname = Field(@subscriber,'LastName')
   Set @Phone = Field(@subscriber, 'Phone')
   /* Query CampaignMember for the appropriate preference campaigns. */
   Set @pageState = 'update'

  elseif @subscriberstatus != 'active' then
   Set @pageState = 'globalsubscribe'
   Set @email = @existingemail

  else /* if ID isn't set*/
  endif

endif

/*Render HTML*/
If @subscriberstatus == 'Active' then]%%
 <h2 style="text-align:center;">Subscription Center</h2>
  <form action="%%=v(@POSTurl)=%%" method="post">
   %%[ If @msg == "success" then ]%%
    <p style="color:green;text-align:center;">Your preferences have been successfully updated!</p>
   %%[Else EndIf]%%
   <input type="hidden" name="id" value="%%=v(@id)=%%">
   <input type="hidden" name="pageState" value="%%=v(@pageState)=%%">
   <input type="hidden" name="CRMobject" value="%%=v(@CRMobject)=%%">
   <input type="hidden" name="existingemail" value="%%=v(@existingemail)=%%">

   <p style="text-align:center;">Email Address: <input type="text" name="emailaddress" size="30" value="%%=v(@email)=%%"></p>
   %%[ If @msg == "invalidemail" then ]%%
    <p style="color:red;text-align:center;">Your email address is invalid. Please correct it, and click Update.</p>
   %%[Else EndIf]%%   
   <p style="text-align:center;">First Name: <input type="text" name="firstname" value="%%=v(@firstname)=%%"></p>
   <p style="text-align:center;">Last Name: <input type="text" name="lastname" value="%%=v(@lastname)=%%"></p>
   <br>

  %%[

  Set @Campaigns = LookupRows("Campaign_Salesforce","Type","Blog Subscriptions")

  If RowCount(@Campaigns) > 0 then
    For @i = 1 to RowCount(@Campaigns) do

    /*Set the id for the campaign */
    Set @campaignid = Field(Row(@Campaigns,@i), "Id")
    Set @campaignname = Field(Row(@Campaigns,@i),"Name")

    Set @CampaignMember = RetrieveSalesforceObjects("CampaignMember",
                                                    "Id,Status",
                                                    Concat(@CRMobject,"Id"),"=",@id,
                                                    "CampaignId","=",@campaignid)

    If RowCount(@CampaignMember) > 0  then
      @IsChecked = "true"
    else 
      @IsChecked = "false"
    endif

    If @i Mod 2 == 1 Then /* If it's an odd number, create a new div. Makes two prefs per line. */
   ]%%
    <div align="left">
    %%[EndIf]%%
            <label style="padding: 0px 50px 0px 50px;">               
            <input type="checkbox" name="Preference_%%=v(@campaignid)=%%" id="Preference_%%=v(@campaignid)=%%" style="vertical-align: middle" %%[If @IsChecked == 'true' Then]%%checked%%[EndIf]%%>&nbsp;&nbsp;<span style="font-size: 16px; color: #000;vertical-align: middle;">%%=v(@campaignname)=%%</span>
            </label>
   %%[If @i Mod 2 == 1 Then /* If it's an odd number, close the div */  
            ]%%</div>%%[  
      endif

    next @i

  endif ]%%

   <br>
   <p style="text-align:center;">Please unsubscribe me from all marketing emails: <input type="checkbox" name="globalunsubscribe">
   <input type="submit" value="Update Preferences"></p>
  </form>

%%[ ELSE ]%%
  <h2 style="text-align:center;">You are currently %%=v(lowercase(@subscriberstatus))=%%!</h2>
  <form action="%%=v(@POSTurl)=%%" method="post">
  <input type="hidden" name="id" value="%%=v(@id)=%%">
  <input type="hidden" name="pageState" value="%%=v(@pageState)=%%">
  <input type="hidden" name="CRMobject" value="%%=v(@CRMobject)=%%">
  <input type="hidden" name="existingemail" value="%%=v(@existingemail)=%%">
  <div class="formgroup2">
  <p style="text-align:center;">
  Want to begin receiving emails again? Click below and your old email preferences will be restored. 
  </p>
  <p style="text-align:center;">Email Address: <input type="text" name="emailaddress" size="30" value="%%=v(@email)=%%"></p>
  %%[ If @msg == "invalidemail" then ]%%
    <p style="color:red;text-align:center;">Your email address is invalid. Please correct it, and click Update.</p>
   %%[Else EndIf]%%
  <p style="text-align:center;"><input type="submit" value="Please include me in future emails"></p>
  </div>
  </form>
%%[ENDIF ]%%